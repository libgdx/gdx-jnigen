def properties = new Properties()
properties.load(ext.propertiesFile.newInputStream())

ext {
    properties.forEach { key, value ->
        ext.setProperty(key as String, value)
    }

    isReleaseBuild = {
        return project.hasProperty("RELEASE")
    }

    getReleaseRepositoryUrl = {
        return project.hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
                : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    }

    getSnapshotRepositoryUrl = {
        return project.hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
                : "https://central.sonatype.com/repository/maven-snapshots/"
    }

    getRepositoryUsername = {
        return project.hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
    }

    getRepositoryPassword = {
        return project.hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
    }
}

subprojects
{
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'java-library'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    version properties.getProperty('version') + (isReleaseBuild() ? "" : "-SNAPSHOT")
    group properties.getProperty('group')

    java {
        withJavadocJar()
        withSourcesJar()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test) {
        systemProperty 'file.encoding', 'UTF-8'
    }

    javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}